#version 330 core
layout (location = 0) in vec3 aPos;
layout (location = 1) in vec3 aNorm;
layout (location = 2) in vec3 aColor;

out vec3 VertColor;

uniform mat4 MVP;
uniform mat4 Model;
uniform mat3 NormalModel;
uniform float AmbientStrength;
uniform float SpecularStrength;
uniform vec3 LightPos;
uniform vec3 LightColor;
uniform vec3 ViewPos;

void main()
{
    gl_Position = MVP * vec4(aPos, 1.0);
    vec3 normal = NormalModel * aNorm;
    vec3 fragPos = vec3(Model * vec4(aPos, 1.0f));
   
    vec3 ambient = AmbientStrength * LightColor;

    vec3 norm = normalize(normal);
    vec3 lightDir = normalize(LightPos - fragPos);
    float diff = max(dot(norm, lightDir), 0.0);
    vec3 diffuse = diff * LightColor;

    vec3 viewDir = normalize(ViewPos - fragPos);
    vec3 reflectDir = reflect(-lightDir, norm);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), 32);
    vec3 specular = SpecularStrength * spec * LightColor;

    VertColor = (ambient + diffuse + specular) * aColor;
}